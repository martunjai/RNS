import("//react-native/common.gni")

config("jsi_config") {
  include_dirs = [
    ".",
    "//react-native/ReactCommon",
  ]

  cflags_cc = [
    "-Wno-extra-semi",
  ]
}

rn_cxx_component("jsi") {
  sources = [
    "jsi/jsi.cpp",
  ]

  with_exceptions = true
  with_rtti = true

  public_configs = [ ":jsi_config" ]
}

rn_cxx_component("JSIDynamic") {
  sources = [
    "jsi/JSIDynamic.cpp",
  ]

  deps = [
    "//folly",
  ]

  public_deps = [
    ":jsi",
  ]

  with_exceptions = true
  with_rtti = true

  public_configs = [ ":jsi_config" ]
}

import("//build/config/linux/pkg_config.gni")
pkg_config("system_javascriptcore") {
    packages = [
     "javascriptcoregtk-4.0",
    ]

}

rn_cxx_component("JSCRuntime") {
  sources = [
    "JSCRuntime.cpp",
  ]

  public_deps = [
    ":jsi",
  ]

  with_exceptions = true
  with_rtti = true

  public_configs = [ ":jsi_config" ]
  defines = [
    "RN_FABRIC_ENABLED",
  ]

  if(is_linux) {
    public_configs += [":system_javascriptcore"]
  } else if (is_mac) {
    frameworks = [
      "JavaScriptCore.framework",
    ]
  }
}
